#!/usr/bin/env perl

use strict;
use warnings;
use Inca::Reporter;

my $reporter = new Inca::Reporter(
  name => 'cluster.admin.softenv.db',
  version => 3,
  description => 'Reports the softenv db available',
  url => 'http://www-unix.mcs.anl.gov/systems/software/msys'
);
$reporter->processArgv(@ARGV);

my @keyXmls;
my $output = $reporter->loggedCommand('softenv');
$reporter->failPrintAndExit("softenv failed: $output $!") if $?;
foreach my $setting(split(/\n/, $output)) {
  next if $setting !~ /\+(\S+)/;
  my $key = $1;
  my @varXmls;
  $output = $reporter->loggedCommand("soft-dec sh add +$key");
  foreach my $line(split(/\n/, $output)) {
    next if $line !~ /^\s*(\S+)\s*=\s*(\S+)/;
    my ($var, $value, $tag) = ($1, $2, 'env');
    if($value =~ s/:\${$var}|\${$var}://g) { # Strip self-reference in $value
      $tag = 'path';
    }
    $value = "$1$ENV{$2}$3" while $value =~ /^(.*)\${([^}]+)}(.*)$/;
    push(@varXmls,
         $reporter->xmlElement($tag, 0,
           $reporter->xmlElement('ID', 1, $var),
           $reporter->xmlElement('value', 1, $value)
         )
       );
  }
  push(@keyXmls,
       $reporter->xmlElement('key', 0,
         $reporter->xmlElement('ID', 1, $key),
         @varXmls)
      );
}
$reporter->failPrintAndExit('no keys available') if $#keyXmls < 0;
$reporter->setBody(
  $reporter->xmlElement('softenv', 0,
    $reporter->xmlElement('ID', '1', 'softenv'),
    @keyXmls
  )
);
$reporter->setResult(1);
$reporter->print();
