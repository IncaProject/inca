#!/usr/bin/env perl

use strict;
use warnings;
use Inca::Reporter::SimpleUnit;
use File::Path;
use Cwd;

my $reporter = new Inca::Reporter::SimpleUnit(
  name => 'grid.middleware.globus.unit.wsgram.jobsubmit',
  version => 7,
  description => "Uses globusrun-ws to submit an executable to a supplied job host and deletes once in queue",
  unit_name => "wsgram_jobsubmit"
);
$reporter->addDependency('sampleGridJob');
$reporter->addDependency('Inca::Reporter::GridProxy');
$reporter->addArg('host', 'job host', 'localhost', '.*');
$reporter->addArg('scheduler', 'scheduler', 'PBS', '.*');
$reporter->addArg('maxMem', 'max memory', '2048', '[0-9]+');
$reporter->addArg('nodes', 'node count', '1', '[0-9]+');
$reporter->addArg('project', 'charge project', '', '|.*');
$reporter->addArg('queue', 'job queue', '', '|.*');
$reporter->addArg('exe', 'job executable', '');
$reporter->processArgv(@ARGV);
my $host = $reporter->argValue('host');
my $scheduler = $reporter->argValue('scheduler');
my $mem = $reporter->argValue('maxMem');
my $nodes = $reporter->argValue('nodes');
my $project = $reporter->argValue('project');
my $queue = $reporter->argValue('queue');
my $exe = $reporter->argValue('exe');

my $workdir = cwd();
if ($exe eq ""){
  $exe="$workdir/var/reporter-packages/build/sampleGridJob";
}
my $tmpdir = "/tmp/grid.middleware.globus.unit.wsgram.jobsubmit.$$";
if ( ! mkpath( "$tmpdir" ) ) {
  failClean("Unable to create temp dir, $tmpdir");
}
$reporter->tempFile($tmpdir);
my $job = "<job>
  <executable>".$exe."</executable>
  <hostCount>$nodes</hostCount>
";
if ($project ne ""){
  $job .= "  <project>$project</project>";
}
if ($queue ne ""){
  $job .= "  <queue>$queue</queue>";
}
$job .= "  <maxTime>5</maxTime>
  <maxMemory>$mem</maxMemory>
</job>";
my $rsl = "$tmpdir/wsgram.rsl";
if ( open R, ">$rsl" ) {
  print R $job;
  close R;
} else {
  failClean("Unable to create temporary file, $rsl");
}
$reporter->log("debug", "$rsl contents:\n $job" );
# -o, -job-epr-output-file filename
# If supplied, the created ManagedJob EPR will be written 
# to the given file following successful submission. 
# The file will not be written if the submission fails.
my $epr = "$tmpdir/wsgram.epr";
my $globusrunws = "globusrun-ws -Ft $scheduler -F https://" .$host. " -submit -b -f $rsl -o $epr";
my $output = $reporter->loggedCommand($globusrunws);
if($?) {
  failClean("$globusrunws failed: $output $!");
} elsif(!-e $epr) {
  failClean("$globusrunws failed: $output");
} else {
  my $globusrunwsKill = "globusrun-ws -kill -job-epr-file $epr";
  $reporter->loggedCommand($globusrunwsKill);
  $reporter->unitSuccess();
  $reporter->print();
}

sub failClean {
  my $err = shift;
  my $pid = $$;
  $err =~ s/$pid/PID/g;
  $err =~ s/inca\.rm\..[^\s\n]*/inca\.rm\.xxx/g;
  $err =~ s/Job ID: .[^\s]+//;
  $err =~ s/Termination time: .[^\n]+//;
  my $uptime = $reporter->loggedCommand("uptime");
  $reporter->log("info", "Uptime: $uptime");
  $reporter->failPrintAndExit($err);
}
